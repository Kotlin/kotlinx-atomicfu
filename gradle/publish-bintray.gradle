// Configures publishing of Maven artifacts to Bintray

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def isNative = project.name.endsWith("native")

// --------------- pom configuration ---------------

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "JetBrains"
            name "JetBrains Team"
            organization "JetBrains"
            organizationUrl "http://www.jetbrains.com"
        }
    }
    scm {
        url "https://github.com/Kotlin/atomicfu"
    }
}

// ------------- tasks

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from "src/main/kotlin"
}

// empty xxx-javadoc.jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "$buildDir/javadoc" // would not exist
}

publishing {
    repositories {
        maven { url = 'https://kotlin.bintray.com/kotlinx' }
    }
    publications {
        maven(MavenPublication) {
            if (!isNative) {
                from components.java
            }
            artifact sourcesJar
            artifact javadocJar
            if (project.name.endsWith("-maven-plugin")) {
                pom.with {
                    packaging = 'maven-plugin'
                }
            }
            pom.withXml {
                def root = asNode()
                // NOTE: Don't try to move top-level things (especially "description") to the pomConfig block
                //       because they would resolve incorrectly to top-level project properties in Gradle/Groovy
                root.appendNode('name', project.name)
                root.appendNode('description', 'AtomicFU utilities')
                root.appendNode('url', 'https://github.com/Kotlin/atomicfu')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    pkg {
        userOrg = 'kotlin'
        repo = 'kotlinx'
        name = 'kotlinx.atomicfu'
        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
}

// TODO :kludge this is required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal
bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it
    }
}
